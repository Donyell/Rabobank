plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'nl.donyell.rabobank'
    compileSdk compile_sdk_version

    defaultConfig {
        applicationId "nl.donyell.rabobank"
        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", "\"https://raw.githubusercontent.com/\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField "String", "SERVER_URL", "\"https://raw.githubusercontent.com/\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.3.2'
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/*'
        }
    }

    testOptions {
        packagingOptions {
            jniLibs {
                useLegacyPackaging true
            }
        }
    }
}

dependencies {
    // Project modules
    implementation project(':presentation')
    implementation project(':domain')
    implementation project(':data')

    // Hilt dependency injection
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Android
    implementation "androidx.core:core-ktx:$kotlin_core_version"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation "androidx.compose.material:material:$compose_ui_version"

    // Coil image
    implementation "io.coil-kt:coil-compose:2.2.2"

    // JUnit 4
    testImplementation "junit:junit:$j_unit_4_version"

    // Mockk
    testImplementation "io.mockk:mockk:$mockk_version"

    // Coroutines test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutines_version"

    // Truth - test assertions
    testImplementation "com.google.truth:truth:$truth_version"

    // Mockk
    androidTestImplementation "io.mockk:mockk-android:$mockk_version"

    // Android JUnit
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"

    // Hilt testing
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_test_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_test_version"

    // Compose debug
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}